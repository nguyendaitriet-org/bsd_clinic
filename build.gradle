import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'com.bsdclinic'
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    version = '1.0.0'

    jar.archiveClassifier.convention('')

    java {
        sourceCompatibility = JavaVersion.VERSION_21
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        implementation {
            exclude group: 'commons-logging', module: 'commons-logging'
        }
    }

    dependencies {
        switch (name) {
            case 'bsdclinic-common': break
            case 'bsdclinic-entity': break
            case 'bsdclinic-email': break
            default: {
                implementation project(':bsdclinic-entity')
                implementation project(':bsdclinic-common')
                implementation project(':bsdclinic-email')
                if (name != 'bsdclinic-security') {
                    implementation project(':bsdclinic-security')
                }
            }
        }

        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-cache:3.4.5'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'org.postgresql:postgresql'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        implementation("org.liquibase:liquibase-core:4.31.1")

        implementation("org.mapstruct:mapstruct:1.6.3")
        annotationProcessor("org.mapstruct:mapstruct-processor:1.6.3")

        implementation("io.jsonwebtoken:jjwt:0.12.6")
        runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.6")
        runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.6")

        implementation 'org.apache.commons:commons-lang3:3.14.0'
        implementation 'commons-beanutils:commons-beanutils:1.9.4'

        implementation 'io.hypersistence:hypersistence-utils-hibernate-60:3.9.4'

        implementation 'software.amazon.awssdk:ses:2.32.9'
        implementation 'software.amazon.awssdk:s3:2.32.9'
    }
}